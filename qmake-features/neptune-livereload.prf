win32|cross_compile: {
    TEMPLATE = aux
    warning("Couldn't generate a neptune wrapper. Please start the example as mentioned in the documentation instead.")
} else {
    WRAPPER_EXT=.sh
    ORIG_TARGET=$$TARGET
    TARGET=$${TARGET}_wrapper$${WRAPPER_EXT}
    CONFIG-=app_bundle

    exists($$[QT_INSTALL_BINS]/qmllivebench):NEPTUNE_LIVERELOAD=$$[QT_INSTALL_BINS]/qmllivebench --importpath $$OUT_PWD/../../../../imports_shared/ --importpath $$PWD/../apps/$$APPLICATION_NAME/ $$HARNESSES_WORKSPACE
    else:NEPTUNE_LIVERELOAD=$$[QT_INSTALL_BINS]/qmlscene -I $$OUT_PWD/../../../../imports_shared/ -I $$PWD/../apps/$$APPLICATION_NAME/ $$HARNESSES_WORKSPACE

    extraplugins.name = QT_PLUGIN_PATH
    extraplugins.CONFIG = append
    extraplugins.value = $$OUT_PWD/../../../../

    stylepath.name = QT_QUICK_CONTROLS_STYLE_PATH
    stylepath.value = $$OUT_PWD/../../../../styles/

    conf.name = QT_QUICK_CONTROLS_CONF
    conf.value = $$OUT_PWD/../../../../styles/neptune/style.conf

    iconpath.name = QT_QUICK_CONTROLS_ICON_PATH
    iconpath.value = $$OUT_PWD/../../../../imports_shared/assets/icons/

    serverconf.name = SERVER_CONF_PATH
    serverconf.value = $$OUT_PWD/../../../../server.conf

    style.name = QT_STYLE_OVERRIDE
    style.value = neptune

    QT_TOOL_ENV = extraplugins stylepath style conf iconpath serverconf
    QT_TOOL_NAME = $$ORIG_TARGET
    qtAddTargetEnv(NEPTUNE_LIVERELOAD, QT)
}
